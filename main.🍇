💭 Include emoji class definition
📜 🔤😶.🍇🔤
🏁 ➡️ 🔢🍇
  🎞 🐇 💻 ❗️ ➡️ args

  ↪️ 📏 args ❓ ◀️ 2 🍇
    😀 🔤Text used to find an emoji must be passed as argument 1🔤 ❗️
    ↩️ 1
  🍉

  📪 🐽 args 1 ❗️ ❗️ ➡️ emojiText
  🍿
    🔤bat🔤 ➡️ 🆕😶 🔤🦇🔤 🍿 🔤holiday🔤 🔤halloween🔤 🍆 ❗️
    🔤candy🔤 ➡️ 🆕😶 🔤🍬🔤 🍿 🔤holiday🔤 🔤halloween🔤 🔤food🔤 🍆 ❗️
    🔤cat-face🔤 ➡️ 🆕😶 🔤🐱🔤 🍿 🔤animal🔤 🍆 ❗️
    🔤coffin🔤 ➡️ 🆕😶 🔤⚰️🔤 🍿 🔤holiday🔤 🔤halloween🔤 🍆 ❗️
    🔤cow-face🔤 ➡️ 🆕😶 🔤🐮🔤 🍿 🔤animal🔤 🍆 ❗️
    🔤dog🔤 ➡️ 🆕😶 🔤🐕🔤 🍿 🔤animal🔤 🍆 ❗️
    🔤dog-face🔤 ➡️ 🆕😶 🔤🐶🔤 🍿 🔤animal🔤 🍆 ❗️
    🔤frog🔤 ➡️ 🆕😶 🔤🐸🔤 🍿 🔤animal🔤 🍆 ❗️
    🔤ghost🔤 ➡️ 🆕😶 🔤👻🔤 🍿 🔤holiday🔤 🔤halloween🔤 🍆 ❗️
    🔤jack-o-lantern🔤 ➡️ 🆕😶 🔤🎃🔤 🍿 🔤holiday🔤 🔤halloween🔤 🍆 ❗️
    🔤penguin🔤 ➡️ 🆕😶 🔤🐧🔤 🍿 🔤animal🔤 🍆 ❗️
    🔤pig-face🔤 ➡️ 🆕😶 🔤🐷🔤 🍿 🔤animal🔤 🍆 ❗️
    🔤santa🔤 ➡️ 🆕😶 🔤🎅🔤 🍿 🔤holiday🔤 🔤christmas🔤 🍆 ❗️
    🔤skull🔤 ➡️ 🆕😶 🔤💀🔤 🍿 🔤holiday🔤 🔤halloween🔤 🍆 ❗️
    🔤spider🔤 ➡️ 🆕😶 🔤🕷️🔤 🍿 🔤holiday🔤 🔤halloween🔤 🔤animal🔤 🍆 ❗️
    🔤spider-web🔤 ➡️ 🆕😶 🔤🕸️🔤 🍿 🔤holiday🔤 🔤halloween🔤 🍆 ❗️
    🔤snake🔤 ➡️ 🆕😶 🔤🐍🔤 🍿 🔤animal🔤 🍆 ❗️
    🔤t-rex🔤 ➡️ 🆕😶 🔤🦖🔤 🍿 🔤animal🔤 🍆 ❗️
    🔤slightly-smiling-face🔤 ➡️ 🆕😶 🔤🙂🔤 🍿 🔤face🔤 🔤smile🔤 🔤creepy🔤 🍆 ❗️
    🔤smirking-face🔤 ➡️ 🆕😶 🔤😏🔤 🍿 🔤face🔤 🔤smug🔤 🔤joke🔤 🍆 ❗️
  🍆 ➡️ emojis

  ↪️ 🐽 emojis emojiText ❗️ ➡️ emoji 🍇
    😀 🖼️ emoji ❓  ❗️
  🍉
  🙅 🍇
    🐙 emojis ❗️ ➡️ keys
    🔂 key keys 🍇
      🍺 🐽 emojis key ❗️ ➡️ emoji
      🐦 emoji emojiText ❓ ➡️ keywordMatch
      ❎ 🔍 key emojiText ❗️ 🙌 🤷 ❗️ ➡️ containedInName
      ↪️ keywordMatch 👐 containedInName 🍇
        😀 🖼️ emoji ❓❗️
      🍉
    🍉
  🍉
  ↩️ 0
🍉

